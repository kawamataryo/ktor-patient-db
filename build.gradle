buildscript {
    ext.kotlin_version = '1.3.11'

    repositories {
        jcenter()
        mavenCentral()
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'

group 'patient-db'
version '0.0.1'
mainClassName = "io.ktor.server.netty.EngineMain"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
    maven { url "http://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url "https://dl.bintray.com/pgutkowski/Maven" }
    maven { url "https://dl.bintray.com/kotlin/squash" }
}

ext {
    ktor_version = '1.0.1'
    logback_version = '1.2.3'
    fuel_version = '1.12.1'
    kgraphql_version = '0.2.5'
    koin_version = '0.8.2'
    squash_version = '0.2.2'
    spek_version = '1.1.5'
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

dependencies {
    // Ktor Server
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "ch.qos.logback:logback-classic:$logback_version"
    compile "io.ktor:ktor-server-core:$ktor_version"
    compile "io.ktor:ktor-gson:$ktor_version"
    compile "io.ktor:ktor-locations:$ktor_version"
    testCompile "io.ktor:ktor-server-tests:$ktor_version"

    // Logging
    compile "ch.qos.logback:logback-classic:$logback_version"

    // Networking
    compile "com.github.kittinunf.fuel:fuel:$fuel_version"

    // GraphQL
    compile "com.github.pgutkowski:kgraphql:$kgraphql_version"

    // Dependency Injection
    compile "org.koin:koin-ktor:$koin_version"

    // Database
    compile "org.jetbrains.squash:squash-h2:$squash_version"

    // Testing
    testCompile 'org.jetbrains.kotlin:kotlin-test'
    testCompile("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime("org.jetbrains.spek:spek-junit-platform-engine:$spek_version")  {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
}

